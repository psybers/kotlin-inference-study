files: output sum[project: string] of int; # count
statements: output sum[project: string] of int; # count
analyzed_commits: output sum[project: string] of int; # number of analyzed commits
project_age: output sum[project: string] of int; # date of earliest commit

{@project-filter@}
{
    visit(input, visitor {
        before node: CodeRepository -> {
            snapshot := getsnapshot(node, "{@file-selector@}");
            foreach (i: int; def(snapshot[i]))
                visit(snapshot[i]);
            stop;
        }
        before node: ChangedFile -> {
            files[input.id] << 1;
        }
        before node: Statement -> {
            statements[input.id] << 1;
        }
    });
    commit_stack: stack of int;
    visit(input, visitor {
        before repository: CodeRepository -> {
            push(commit_stack, repository.head);
            visit(getrevision(repository, repository.head));
            stop;
        }
        after repository: CodeRepository -> {
            analyzed_commits[input.id] << len(commit_stack);
            if (len(commit_stack) > 0) {
                earliest_commit := getrevision(repository, pop(commit_stack));
                project_age[input.id] << T"{@release-date@}" - earliest_commit.commit_date;
            }
            clear(commit_stack);
        }
        before rev: Revision -> {
            if (len(rev.parents) > 0) {
                push(commit_stack, rev.parents[0]);
                visit(getrevision(current(CodeRepository), rev.parents[0]));
            }
            stop;
        }
    });
}

